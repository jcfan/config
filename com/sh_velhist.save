#!/bin/csh -f
#
#doc Make a histogram of cumulative residuals from single-column file
#doc
#    
if ( $#argv == 0  ) then
echo "  "
echo " Script to create a histogram of velocity residuals using a GLOBK prt file as input."  
echo " " 
echo "   The default is to plot the cumulative histogram of residuals and together "
echo "   with the theoretical chi curve of probabilities for either the 1-d (E,N,H) "
echo "   or 2-d (horizontal velocity magnitude) chi-square distribution "
echo " "
echo "    sh_velhist -f <file> -ps <psfile> -c <comp> -sig <maxsig> -rat <maxrat> -vel <maxvel> -noabs -nocum -val -noerase "   
echo " "
echo "      file   : Input GLOBK prt, org or vel" 
echo " "
echo "      psfile : Name for output postscript file "
echo " " 
echo "      comp   : Component to extract < E N H or V > where V (default) is the horizontal magnitude "
echo "  "       
echo "      maxsig : Maximum sigma of the selected component to include (default 10 mm/yr)"
echo "  "
echo "      maxrat : Maximum ratio (value/sigma) to include (default 3)" 
echo "  "
echo "      maxvel : Maximum absolute value of the residual velocity to include (default 10 mm/yr)" 
echo "  "
echo "      -noabs : Don't take the absolute value (E, N, H); default is absolute value "
echo " "
echo "      -nocum : Plot the actual (not cumulative) distribution (vertical axis is number, not percent"
echo " "     
echo "      -val   : Plot the actual residuals (mm) rather than the ratio of residual to uncertainty"    
echo " "  
echo "      -noerase: Do not remove the outhist, tmp.chivals, and vel2hist.rms files "  
echo " "
echo "      -scalesig: Scale the uncertainties by this factor (default 1.0)"
echo " "  
echo "  Requirements:  GMT and the gamit/utils programs vel2hist and chicurve"
echo " "   
exit
endif
 
## Set the defaults

set pshist = ""
set cum = "yes"
set absv = "absv"
set ratio = "ratio"   
set comp = "V"  
set pshistfile = "velhist.ps"
set maxsig = 10
set maxrat = 3
set maxvel = 10   
set erase = "yes" 
set scalesig = 1.

echo 
      
## Decode the command line

while ($#argv > 0 ) 
set input = ( $argv ) 
switch($input[1])
	case -f:
		set file  =  $input[2]
		breaksw    
	case -ps:
		set pshistfile  =  $input[2].ps
		breaksw
	case -c:
		set comp =  $input[2]
		breaksw 
   case -sig:
       set maxsig = $input[2]
       breaksw
   case -rat:
       set maxrat = $input[2]
       breaksw   
   case -vel:
       set maxvel = $input[2]
       breaksw
   case -noabs:
       set absv = "sign" 
       breaksw
   case -nocum:
       set cum = "no" 
       breaksw
   case -val:
       set ratio = "value"  
       breaksw          
   case -noerase:
       set erase = "no"  
       breaksw   
   case -scalesig:
       set scalesig = $input[2]
       breaksw


endsw 
shift argv 
end
     
# Get single-column of values for pshistogram by extracting the right quantity from the vel file

vel2hist $file outhist $comp $maxsig $maxvel $maxrat $absv $ratio $scalesig
       
if( $cum == "no" ) then
                 
  pshistogram outhist -L -P -C -W0.2 -Jx1./.1 -P -K >! $pshistfile
#         NOTE: for the non-cumulative case, we may need to add logic to chose the W and J values
  

else

  pshistogram outhist -Q -Z1 -L -C -W0.1 -Jx1./.05 -Bf0.5/f5a20 -P -R0/$maxrat/0/100 -K >! $pshistfile 
#  add the theoretical chi curve
  set numpts = 100
  if( $comp == "V" ) then
     set ndf = 2
  else
     set ndf = 1
  endif          
  chicurve $ndf $numpts >! tmp.chivals
  psxy tmp.chivals -R0/$maxrat/0/100 -Jx1./.05 -Bf0.5/f5a20 -V -O -P -K >>  $pshistfile    

endif                                                                   

# Add labels
pstext << ENDD -R0/8.5/0/11 -Jx1/1 -V -O -P -K >> $pshistfile
4 5  10 0 0 9 Solution $file   
ENDD
pstext << ENDD -R0/8.5/0/11 -Jx1/1 -V -O -P -K >> $pshistfile
4 4.5  10 0 0 9 Component $comp   
ENDD
cat vel2hist.rms           
if ( -e vel2hist.rms ) then   
  set text = ( `cat vel2hist.rms` )
  pstext << ENDD -R0/8.5/0/11 -Jx1/1 -V -O -P >> $pshistfile
4 4.0  10 0 0 9  $text
ENDD
endif


# Erase the temporary files
if( $erase == "yes" ) then
 \rm outhist
 \rm tmp.chivals   
 \rm vel2hist.rms
endif

echo " "
echo " Created postscript file : " $pshistfile      
echo " " 

exit
