#!/bin/csh -f 
#
#doc Update station.info using header infomation from rinex files
#doc
#
# Modified  by rwk 2002/03/11 to use single program (mstinf2) for both updates and merges
#   and to use the new station.info format   
# Modified by rwk 2007/1/4 to fix bug: formerly would not update existing entries with 
#   new information for the same span        
# Modified by rwk 2008/10/21 to use new 'mstinf' for new-format files (replaces mstinf2; old-format
#   files no longer supported so old 'mstinf' now obsolete.  Add reading of IGS log and SINEX files;
#   fix nosort option; add all other mstinf options previously not in shell script.


######################## INSTRUCTIONS #############################
# See if arguments passed
if( $#argv == 0 ) then
  echo "===================================================================================="
  echo " Update station.info using header infomation from RINEX file " 
  echo "                           and/or"
  echo " Update station.info by merging and sorting entries from other station.info's"
  echo "  "
  echo " Usage: sh_upd_stnfo -ref <rsfile> -l <sitelist> -merge <sfiles> -files <rfiles> -i <ifiles> -x <snxfiles>  -xs <snxsite>  -w <outfile> -l <sitelist>  -nosort -c  -h <slant hgt> -t <dup_tol> "
  echo " "          
  echo "        <rsfile>  Reference station.info (defines output info and format). [default station.info]"
  echo "               If not given and station.info does not exist, a new station.info file will be created with the format:" 
  echo "*SITE  Station Name      Session Start      Session Stop       Ant Ht   HtCod  Ant N    Ant E    RcvCod  SwVer  AntCod" 
  echo " "                     
  echo "        <sitelist> file containing list of stations to be included from reference station.info (optional; default all; format columns 1-4) "
  echo " "
  echo "        <rfiles> RINEX file/s to be used to update station.info (wildcards allowed)"
  echo " "
  echo "        <sfiles> station.info files to be merged with the reference file"
  echo "  "    
  echo "        <ifiles> IGS log files to be used to update station.info."  
  echo " "
  echo "        <snxfiles > SINEX files to be used to update station.info; see -xs"
  echo "        <snxsite>   site from SINEX file to be added; default is to add all sites"
  echo " "
  echo "        <outfile> new station.info from a merge (default station.info.new, renamed to station.info) " 
  echo " "
  echo "        -nosort option not to sort the output station.info (use only when confident of time order and no duplicates)"
  echo " "             
  echo "        -c  Copy all comments to the new station.info"
  echo " " 
  echo "        <slant hgt> antenna height (m) above which the height in the rinex will be"
  echo "               as a slant height.  NOTE: Use with caution; not consistent with"
  echo "               RINEX standard and is included only for non-standard RINEX files"
  echo " "
  echo "        <dup_tol>  Tolerance in seconds for deciding whether two entries are the same (default 120s)"
  echo " "                  
  echo "    NOTES:  There are three modes: "
  echo "             (1) Stand-alone to merge station.info files, one or more inputs, one output"
  echo "                  sh_upd_stnfo -ref station.info.template -merge station.info.s2 -w station.info.s3 "
  echo "                    (default output is 'station.info') "
  echo "             (2) Stand-alone to incorporate RINEX, IGS log, or SINEX information into station.info, one or more files"
  echo "                     (input and output files are 'station.info', continually updated) "     
  echo "                  sh_upd_stnfo -f *.99o  (input and output files are station.info, continually updated) "
  echo "             (3) Within sh_gamit, adding information from one RINEX file at a time within a session"
  echo "                     (input 'station.info', output to 'station.info.new' renamed to 'station.info' "
  echo "                       if successful, origional saved as 'station.info.new') "
  echo "  "                  
  echo "     RESTRICTION: RINEX, SINEX, or IGS log files must be in the current directory"
  echo "===================================================================================="
  exit
endif 

##################### SET UP DEFAULTS #############################   
# Setup necessary paths and filenames

set files = ''
set ref = ''    
set igslog = ''
set sinex = '' 
set snxsite = ''  
set snxsitecmd = ''
set merge = ''
set orbt = ''
set unique = ''    
set nopt = ''
set nosort = ''  
set sitelistcmd = ''
set comments = ''
set slhgt  = '0.00'
set outfile = 'station.info.new'
set ref = 'station.info'
set mailto = `whoami`
set machine = `hostname`

##################### DECIPHER COMMAND LINE #######################   
while ($#argv > 0 )
  set input = ( $argv )
  switch($input[1])
    case -f*:
      set files  = (`echo $argv | cut -d- -f2`); shift files
    breaksw 
    case -m*:
      set merge  = (`echo $argv | cut -d- -f2`); shift merge
    breaksw 
    case -r*:
      set ref  = $input[2]  
    breaksw 
    case -i:
      set igslog = $input[2]
    breaksw
    case -x:
      set sinex = $input[2]
    breaksw     
    case -xs:    
      set snxsite = $input[2]
      set snxsitecmd = "-xs $input[2]"
    breaksw      
    case -l:                              
       set sitelistcmd = "-l $input[2]"  
    breaksw
    case -co*:
      set comments = '-c'
    breaksw
    case -n:
      set nopt = "-n"     
    breaksw
    case -no*:
      set nosort = "-ns"
    breaksw 
    case -h
       set slhgt = $input[2]
    breaksw
    case -w
       set outfile = $input[2]
    breaksw                       
  endsw
  if ( $#argv > 0 ) shift argv
end
alldone:
##################### GET THE JOB DONE ############################     
# Set timestamp hostname variable
set ts = "`hostname`:`date +"%H%M%S"`"

echo "EXECUTING sh_upd_stnfo"

if ( ! -e guess_rcvant.dat ) ln -s ~/gg/tables/guess_rcvant.dat .
                         
# Create station.info if it does not exist and a reference file is not input    
if ( ! -e station.info && $ref == '' ) then
  echo station.info does not exist. Creating a new. 
  set ref = 'station.info' 
#station.info doesn't exist (create an empty station.info with header)
  echo "*          Gamit station.info"                >! station.info
  echo "*"                                            >> station.info
  echo "*          Generated by sh_update_stnfo: $ts" >> station.info
  echo "*"                                            >> station.info
  echo "*SITE  Station Name      Session Start      Session Stop       Ant Ht   HtCod  Ant N    Ant E    RcvCod  SwVer  AntCod" >> station.info
  #echo "*SITE  Station Name      Session Start      Session Stop       Ant Ht   HtCod  Ant N    Ant E    Receiver Type         Vers                  SwVer  Receiver SN           Antenna Type     Antenna SN" >> station.info         
endif

# Copy original station.info for safe keeping.
if ( ! -e station.info.orig ) then
  echo "Saving original station.info to station.info.orig " 
  \cp station.info station.info.orig
endif
   
# Detect old-style station.info  (no longer supported)
grep 'TRCK SITE' station.info >! tmp.stinfo
set num = `wc tmp.stinfo` 
if( $num[1] > 0 ) then  
   echo "Old-style station.info no longer supported: use conv_stnfo to convert"
   exit
endif     
                     

# Do station.info merging first if requested.
if ( $merge[1] != '' ) then
   echo "Updating $ref using information from $merge " 
   echo "Output file is $outfile"
   mstinf -f ${ref} -w ${outfile} -s ${merge} -o $nosort $comments ${sitelistcmd} 
   echo "Output file is $outfile"
endif

# Now add entries from an IGS log file
if ( $igslog  != '' ) then
   echo "Updating $ref using information from $igslog "   
   mstinf -f ${ref} -w ${outfile} -i ${igslog} -o  $nosort $comments ${sitelistcmd}  
   echo "Output file is $outfile"
endif

# Now add entries from a SINEX log file
if ( $sinex != '' ) then    
   if( $snxsitecmd == '' ) then
     echo "Updating $ref for all sites in SINEX file $sinex "   
   else
     echo "Updating $ref for $snxsite in SINEX file $sinex "
   endif     
   echo "Output file is $outfile"
   mstinf -f ${ref} -w ${outfile} -x ${sinex} ${snxsitecmd} -o  $nosort $comments ${sitelistcmd}
endif
       
# Make sure the 'problems' file exists for concatenation
if( ! -e MSTINF.problems ) then
   touch MSTINF.problems 
endif 

# Loop over rinex files adding station.info entries as we go.
foreach file ( $files )
  
  echo "Updating station.info using information from RINEX file $file " 
  mstinf -f station.info -w station.info.new -r $file -o 
  if ( -e MSTINF.warning ) then
    cat MSTINF.warning  >> MSTINF.problems  
    cat MSTINF.warning >> GAMIT.warning
    \rm -r MSTINF.warning 
  endif   
  if( -e MSTINF.fatal ) then
    cat MSTINF.fatal >> MSTINF.problems
    if( ! -e GAMIT.fatal ) then
      cat MSTINF.fatal > GAMIT.fatal
    else
      cat MSTINF.fatal >> GAMIT.fatal
    endif
  endif
# Have a cup of coffee and let NFS complete any buffered writes.
  sleep 3

# Move new station.info into place..
# See type of file (if it is a link then copy into link)
  set lnk = `\ls -l station.info | awk '{print substr($0,1,1)}'`
  if( $lnk == 'l' ) then
      set lfname = `\ls -l station.info | awk '{print $NF}'`
  else 
      set lfname = "station.info"         
  endif
             
           
# Skip the rename to station.info if the output file is explictly specified 
  if( $outfile == 'station.info.new' ) then 
#   If upd_stnfo ran successfully this file should be longer than 3 lines    
    if( -e station.info.new ) then 
      set num = `wc station.info.new` 
      if( $num[1] > 3 ) then
        echo Updating $lfname 
        \mv station.info.new $lfname
#     Everything seems to have worked so now remove the original
#    (This ensures that the next run will use the current station.info)
      \rm station.info.orig >& /dev/null
      else    
        echo MSTINF failed, restoring original $lfname
        \mv station.info.orig $lfname 
      endif
    else
      echo MSTINF failed, restoring original $lfname
      \mv station.info.orig $lfname 
    endif  
  endif 

# End of loop on RINEX files
end

# Thats it!!
exit
