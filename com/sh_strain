#!/bin/csh

if ( $#argv == 0) then
echo "  "
echo " Script to read a 13-column velocity file and calculate various strain components on a map."
echo " "
echo " Basic usage : $0:t my_velocities.vel"
echo                ""
echo "               $0:t ~/gg/Calif_example/eq_disp.globk_vel_021102c.org.vel"
echo " "
echo " When successfully run, you should get 12 single-screen displays by cycling through"
echo " the ps file produced, each based on computation of the strain tensor for Delaunay triangles:"
echo "   E, N, U velocity in mm/yr (showing changes across the network) "
echo "   E-ward and N-ward gradient of N velocity in ppm/yr"
echo "   E-ward and N-ward gradient of E velocity in ppm/yr"
echo "   areal dilatation in ppm/yr"  
echo "   clockwise spin rate in ppm/yr"
echo "   gamma1 in microradian/yr" 
echo "   gamma2 in microradian/yr"  
echo "   gamma magnitude in microradians/yr"
echo " "         
echo "Last updated 20090421 Kurt Feigl "
exit
endif

#  20090421 Kurt 
#     Add the -A switch to psxy tmp.res to plot Delaunay triangulation as straight lines, 
#     rather than arcs of great circles. The gridding is done using straight lines.

# Here is a test data file
#cat - <<EOF >! model.vel
# -120.616 34.556 -10.6559 12.3677 -10.6559 12.3677  1.0 1.0  0.00  -0.2025 -0.2025  1.0  VNDN
# -119.301 34.636 -10.6138 6.7551 -10.6138 6.7551  1.0 1.0  0.00  1.5249 1.5249  1.0  MUNS
# -119.394 35.398 5.7464 -5.5411 5.7464 -5.5411  1.0 1.0  0.00  0.5833 0.5833  1.0  FIBR
# -119.484 34.853 -10.2407 4.6688 -10.2407 4.6688  1.0 1.0  0.00  2.1425 2.1425  1.0  YAM2
# -119.714 34.494 -11.0196 9.4600 -11.0196 9.4600  1.0 1.0  0.00  -0.0257 -0.0257  1.0  LACU
# -119.965 34.723 -10.9290 10.1892 -10.9290 10.1892  1.0 1.0  0.00  0.4738 0.4738  1.0  FIGP
# -120.067 35.076 -8.1733 9.6154 -8.1733 9.6154  1.0 1.0  0.00  0.3874 0.3874  1.0  MADC
# -120.199 34.502 -10.8139 11.1964 -10.8139 11.1964  1.0 1.0  0.00  -0.0930 -0.0930  1.0  GAVI
# -120.257 34.799 -10.4383 11.3277 -10.4383 11.3277  1.0 1.0  0.00  0.2876 0.2876  1.0  ALAM
# -120.267 34.691 -10.7246 11.3521 -10.7246 11.3521  1.0 1.0  0.00  0.1701 0.1701  1.0  RDRK
# -120.299 35.346 -6.7769 9.5842 -6.7769 9.5842  1.0 1.0  0.00  0.2109 0.2109  1.0  POZO
# -120.300 34.960 -9.6883 11.3768 -9.6883 11.3768  1.0 1.0  0.00  0.3204 0.3204  1.0  LIND
# -120.414 34.731 -10.5412 11.7851 -10.5412 11.7851  1.0 1.0  0.00  0.1115 0.1115  1.0  GRAS
# -120.495 34.767 -10.4052 12.0101 -10.4052 12.0101  1.0 1.0  0.00  0.0928 0.0928  1.0  FARF
# -120.525 34.609 -10.6857 12.1168 -10.6857 12.1168  1.0 1.0  0.00  -0.1000 -0.1000  1.0  SOAP
# -120.606 34.894 -9.8098 12.3056 -9.8098 12.3056  1.0 1.0  0.00  0.1708 0.1708  1.0  LOSP
# -120.832 35.359 -8.6309 12.9003 -8.6309 12.9003  1.0 1.0  0.00  0.0108 0.0108  1.0  BLHL
# -121.284 35.665 -8.2722 13.7952 -8.2722 13.7952  1.0 1.0  0.00  -0.3802 -0.3802  1.0  BLAN
# -115.000 32.000 -5.5227 7.1694 -5.5227 7.1694  1.0 1.0  0.00  0.2914 0.2914  1.0  A1
# -115.000 39.000 11.4705 -11.4673 11.4705 -11.4673  1.0 1.0  0.00  0.4361 0.4361  1.0  a2
# -122.000 32.000 -8.6239 11.0083 -8.6239 11.0083  1.0 1.0  0.00  -0.5216 -0.5216  1.0  a3
# -122.000 39.000 10.1286 -9.8802 10.1286 -9.8802  1.0 1.0  0.00  -0.5049 -0.5049  1.0  a4
#EOF


set VFILE = $1
set TAG = $VFILE:h
set OUT = $VFILE:t.str.ps

# delete headers
grep -v deg $VFILE | grep -v Lat | awk 'NF==13{print $0}' >! tmp.vel

# This script will fail files with fewer than 5 stations (3 triangles)
set NL = `wc -l tmp.vel | awk '{print $1}'`
if (${NL} < 5) then
   echo "ERROR: Only $NL stations in velocity file. This is not enough. Stopping here."
   exit -1
endif


# for degrees
set PROJ = -JM15
#Coarse grids for old, 20-th century computers
#set GRDSTEPX = 0.1
#set GRDSTEPY = 0.1
# finer resolution
set GRDSTEPX = 0.01
set GRDSTEPY = 0.01
#set CBARFMT = "%13.7le"  # change this if you get error messages like the one below
set CBARFMT = "%11.5g"  # change this if you get error messages like the one below
#grdimage: GMT Fatal Error: Z-slice with dz = 0

set TICKS = -Bf10ma60m/f10ma60m
echo $TICKS
set RANGES = `minmax -I0.1/0.1 tmp.vel`

if (-e .gmtdefaults)  \rm -f .gmtdefaults
if (-e .gmtdefaults4) \rm -f .gmtdefaults4
gmtset PAPER_MEDIA letter+
gmtset MEASURE_UNIT cm
gmtset DEGREE_FORMAT 3
gmtset TICK_LENGTH 0.2
gmtset ANOT_FONT_SIZE 12
gmtset LABEL_FONT_SIZE  12
gmtset HEADER_FONT_SIZE 12
gmtset BASEMAP_TYPE FANCY
gmtset VERBOSE TRUE


# Extract velocities in proper format for psvelo
awk 'NF == 13 {print " ",$1,$2,$3,$4,$7,$8,$9}' tmp.vel >! tmp.velo 

# make Delaunay triangles
#awk 'NR > 3{print $1,$2}' $VFILE | sort -n | triangulate -H0 $RANGES -M $PROJ | mapproject $PROJ $RANGES -I -M >! tmp.res
#awk 'NF == 13{print $1,$2}' tmp.vel |  triangulate -H0 $RANGES -M $PROJ -I$GRDSTEPX/$GRDSTEPY  >! tmp.res
gmtset D_FORMAT %20.10E
awk 'NF == 13{print $1,$2,0}' tmp.vel |  triangulate -H0 $RANGES -M $PROJ -I$GRDSTEPX/$GRDSTEPY -Gtmp.grd >! tmp.res

# East Velocity in mm/yr
awk 'NF == 13 {print $1,$2,$3}' tmp.vel | triangulate -H0 $RANGES $PROJ -I$GRDSTEPX/$GRDSTEPY -Ge.grd 
grdedit e.grd -Ddeg/deg/mm_per_yr/1./0./east_velocity/$TAG
grdinfo e.grd

# North Velocity in mm/yr
awk 'NF == 13 {print $1,$2,$4}' tmp.vel | triangulate -H0 $RANGES $PROJ -I$GRDSTEPX/$GRDSTEPY -Gn.grd 
grdedit n.grd -Ddeg/deg/mm_per_yr/1./0./north_velocity/$TAG
grdinfo n.grd

# Upward Velocity in mm/yr
awk 'NF == 13 {print $1,$2,$10}' tmp.vel | triangulate -H0 $RANGES $PROJ -I$GRDSTEPX/$GRDSTEPY -Gu.grd 
grdedit u.grd -Ddeg/deg/mm_per_yr/1./0./up_velocity/$TAG
grdinfo u.grd

# gradients in ppm/yr
# mm/yr/m * -1.e3 
# The minus sign is because of the following "gotcha" in grdgradient:
#     derivative is negated so that grdgradient will give positive
#     values when the slope of z(x,y)  is  downhill  in  the  azim
#     direction.  


grdgradient e.grd -A90. -Gtmp.grd -M
grdmath tmp.grd -1.e3 MUL = dvede.grd
grdedit dvede.grd -D=/=/ppm_per_yr/=/=/"Eastward gradient of East component"/=
grdinfo dvede.grd

grdgradient n.grd -A90. -Gtmp.grd -M
grdmath tmp.grd -1.e3 MUL = dvnde.grd
grdedit dvnde.grd -D=/=/ppm_per_yr/=/=/"Eastward gradient of North component"/=
grdinfo dvnde.grd

grdgradient e.grd -A0. -Gtmp.grd -M
grdmath tmp.grd -1.e3 MUL = dvedn.grd
grdedit dvedn.grd -D=/=/ppm_per_yr/=/=/"Northward gradient of East component"/=
grdinfo dvedn.grd

grdgradient n.grd -A0. -Gtmp.grd -M
grdmath tmp.grd -1.e3 MUL = dvndn.grd
grdedit dvndn.grd -D=/=/ppm_per_yr/=/=/"Northward gradient of North component"/=
grdinfo dvndn.grd

grdmath dvede.grd dvndn.grd ADD = dil.grd
grdedit dil.grd -D=/=/ppm_per_yr/=/=/"Areal dilatation"/=

grdmath dvedn.grd dvnde.grd SUB = rot.grd
grdedit rot.grd -D=/=/ppm_per_yr/=/=/"Clockwise Spin rate"/=

grdmath dvede.grd dvndn.grd SUB = gam1.grd
grdedit gam1.grd -D=/=/microrad_per_yr/=/=/"Gamma 1"/=

grdmath dvnde.grd dvedn.grd ADD = gam2.grd
grdedit gam2.grd -D=/=/microrad_per_yr/=/=/"Gamma 2"/=

grdmath gam1.grd gam2.grd HYPOT = gamt.grd
grdedit gamt.grd -D=/=/microrad_per_yr/=/=/"Gamma Total"/=

gmtset D_FORMAT %.2F ; grd2cpt e.grd >! tmp.cpt
grdimage e.grd -Ctmp.cpt $PROJ ${TICKS}":.East Velocity in mm/yr:"/WeSn -P -K -X3 -Y3 >! $OUT
psscale -Ctmp.cpt -D15/8/10/1 -K -O -P >> $OUT

#psvelo tmp.velo -Se0.05/0.95/9 $PROJ $RANGES -A0.05/0.15/0.07 -V -O -K -P >> $OUT
psxy tmp.res -A $PROJ $RANGES -V -M -O -U$TAG -P -K >> $OUT
psxy tmp.velo -Sc0.1 $PROJ $RANGES -G255 -V -O  -P >> $OUT

gmtset D_FORMAT %.2F ; grd2cpt n.grd >! tmp.cpt
grdimage n.grd -Ctmp.cpt $PROJ ${TICKS}":.North Velocity in mm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/8/10/1 -K -O >> $OUT
psvelo tmp.velo -Se0.05/0.95/9 $PROJ $RANGES -A0.05/0.15/0.07 -V -O -K -P >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt n.grd >! tmp.cpt
grdimage u.grd -Ctmp.cpt $PROJ ${TICKS}":.Upward Velocity in mm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/8/10/1 -K -O >> $OUT
psvelo tmp.velo -Se0.05/0.95/9 $PROJ $RANGES -A0.05/0.15/0.07 -V -O -K -P >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

#grdimage r.grd -Cvel.cpt $PROJ ${TICKS}":.Line of site comp in mm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
#psscale -Cvel.cpt -D15/8/10/1 -K -O >> $OUT
##psvelo tmp.velo -Se0.05/0.95/9 $PROJ $RANGES -A0.05/0.15/0.07 -V -O -K -P >> $OUT
#psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT
#
#grdimage f.grd -Cfringe.cpt $PROJ ${TICKS}":.Fringes over 5 years:"/WeSn -P -K  -X3 -Y3  >> $OUT
#psscale -Cfringe.cpt -D15/8/10/1 -K -O >> $OUT
##psvelo tmp.velo -Se0.05/0.95/9 $PROJ $RANGES -A0.05/0.15/0.07 -V -O -K -P >> $OUT
#psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT


gmtset D_FORMAT ${CBARFMT} ; grd2cpt dvede.grd >! tmp.cpt
grdimage dvede.grd -Ctmp.cpt $PROJ ${TICKS}":.E-ward grad of Ve in ppm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt dvnde.grd >! tmp.cpt
grdimage dvnde.grd -Ctmp.cpt $PROJ ${TICKS}":.E-ward grad of Vn in ppm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt dvedn.grd >! tmp.cpt
grdimage dvedn.grd -Ctmp.cpt $PROJ ${TICKS}":.N-ward grad of Ve in ppm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt dvndn.grd >! tmp.cpt
grdimage dvndn.grd -Ctmp.cpt $PROJ ${TICKS}":.N-ward grad of Vn in ppm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt dil.grd >! tmp.cpt
grdimage dil.grd -Ctmp.cpt $PROJ ${TICKS}":.Areal dilatation in ppm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt rot.grd >! tmp.cpt
grdimage rot.grd -Ctmp.cpt $PROJ ${TICKS}":.Clockwise Spin Rate in ppm/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt gam1.grd >! tmp.cpt
grdimage gam1.grd -Ctmp.cpt $PROJ ${TICKS}":.Gamma1 in microrad/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt gam2.grd >! tmp.cpt
grdimage gam2.grd -Ctmp.cpt $PROJ ${TICKS}":.Gamma2 in microrad/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT

gmtset D_FORMAT ${CBARFMT} ; grd2cpt gamt.grd >! tmp.cpt
grdimage gamt.grd -Ctmp.cpt $PROJ ${TICKS}":.Gamma Total in microrad/yr:"/WeSn -P -K  -X3 -Y3  >> $OUT
psscale -Ctmp.cpt -D15/10/18/1 -K -O >> $OUT
psxy tmp.res -A $PROJ $RANGES -M -O -U$TAG -P >> $OUT




gs $OUT



